// ##################
// Forge Build Script
// ##################

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        //jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

// ##############################
// Versions & Other Configuration
// ##############################

/**
 * Holds values needed to run/build Shoppery that are likely to change. Such as versioning.
 */
final class BuildProperties {
    //Don't forget to change version numbers in
    //META-INF/mods.toml  &  ShopperyMod.java as well

    static TITLE = 'ShopperyCraft'

    static VERSION = '1.0.0'

    static MODID = 'shoppery'

    static AUTHOR = 'Ki11er_wolf'

    static MINECRAFT_VERSION = '1.16.5'

    static FORGE_VERSION = '36.1.0'

    static MAPPINGS_VERSION = '20201028-1.16.3'

    static DOMAIN = 'com.ki11erwolf' + '.' + MODID

    static GString MCFORGE_VERSION = "${MINECRAFT_VERSION}-${FORGE_VERSION}"
}

//Standard forge build script
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = BuildProperties.VERSION
group = BuildProperties.DOMAIN
archivesBaseName = BuildProperties.MODID

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {

    mappings channel: 'snapshot', version: BuildProperties.MAPPINGS_VERSION

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                shoppery {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                shoppery {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:" + BuildProperties.MCFORGE_VERSION
}

jar {
    manifest {
        //noinspection SpellCheckingInspection
        attributes([
            "Specification-Title": BuildProperties.MODID,
            "Specification-Vendor": BuildProperties.AUTHOR,
            "Specification-Version": BuildProperties.VERSION + "-" + BuildProperties.MCFORGE_VERSION,

            "Implementation-Title": BuildProperties.TITLE,
            "Implementation-Vendor" : BuildProperties.AUTHOR,
            "Implementation-Version": BuildProperties.VERSION,

            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
